clear // 메모리에 입력된 stata 데이터(dta파일)를 메모리에서 지우는 명령어
set more off //결과창에 결과가 끊김없이 나오게끔 해주는 명령어
browse //데이터 브라우져가 뜨게금(편집불가능모드임) 하는 명령어

*cd " 너님 디렉토리" // 파일을 읽고 쓰는 곳 지정
 
********************
* (1) import 
********************
import excel "예제파일.xlsx",clear
ren *,lower // 모든변수 대문자-> 소문자로 
drop o p q //잡스런 변수 제거
 
replace a=subinstr(a,char(10),"|",.) 
/*엑셀의 줄바꿈(line break)은 문자의 한종류로 컴퓨터가 인식함, 따라서 이를  이를 |로 바꾸어주기 */
nrow 2, keep 

drop in 2 //두번째줄 제거
 
save 예제.dta,replace //저장
 
********************
 
********************
* (2) 패널 롱폼 구축
********************
 
* 점수, 문항 ,단원 transpose하기
 
u 예제.dta,clear //로드
replace a="단원" in 2 // 둘째줄의 a의 변수값을 단원으로 바꾸기
keep in 1/2 //1번째 줄에서 2번째줄 남기기
 
*-----------------------
* reshape의 응용: transpose
*-----------------------
 
reshape long _ ,i(a) j(문항) 
reshape wide _,i(문항) j(a) string //a는 문자변수(string)이기에 string옵션을 반드시 넣어 줘야 함.
 
*-----------------------
 
ren _* * // 변수명 _로 시작되는 모든 변수들을 대상으로  _를 때어내기
destring 점수,replace //점수변수를 string에서 숫자변수로 바꾸기
 
save temp.dta,replace //임시파일저장. 나중에 병합(merge할 때 사용할 것임)
 
* 아이들 채점결과 롱폼으로 바꾸기.
 
u 예제.dta,clear //로드
drop in 1/2 //이번엔 1번째 줄에서 2번째줄 제거
ren _* 문항* //변수명 _로 시작되는 모든 변수들을 대상으로 앞자리가 문항으로 시작하게끔 바꾸기
reshape long 문항,i(a) j(time) //패널 롱폼으로 
ren 문항 맞틀 //문항변수의 이름을 맞틀로 바꾸기
ren time 문항 //time변수 이름을 문항으로 바꾸기
 
* 상기 임시파일과 merge하기
 
merge m:m 문항 using temp.dta //상기 임시저장파일(temp.dta)을 병합하기
drop _merge // _merge 변수 삭제, merge할 때부터 안생기게 하고 싶으면 nogen 옵션 추가하면 됨
ren a 이름
sort 이름 문항 //이름 및 문항으로 오름차순 정렬
 
* 맞틀 점수를 더미화 하기
tab 맞틀,gen(d) //맞틀에 대한 두 종류의 더미변수 생성
drop 맞틀 //맞틀 지우기, 우리가 원하는건 채점결과를 계산하기위한 더미변수를 필요로 하기 때문
ren d1 맞틀 //d1변수를 맞틀 변수로... o가 1, x가 0이니까
drop d2 // d2 지우기 필요 없음
 
label var 맞틀 "맞으면 1, 틀리면 0" //맞틀변수 라벨넣어주기
 
 
save 예제.dta,replace
 
**************************
 
****************************
* (3) 이름 및 단원 별 채점점수 도출
****************************
 
* 채점결과 도출하기
 
u 예제.dta,clear
gen 채점결과=점수*맞틀 //채점결과 게산, [채점결과=(점수)x(맞틀)]
 
* (이름, 단원별) 채점점수 도출
 
collapse (sum) 채점=채점결과 ,by(이름 단원) //이름 및 단원별로 점수 산출
replace 단원=subinstr(단원,"단원","",.) //단원변수의 변수값내의 단원이란 문자 제거하기
 
reshape wide 채점,i(이름) j(단원) string 
/*옆으로 바꿔주기, 단원변수는 문자로 인식된 string변수이므로 string 옾션을.. */
ren 채점* 단원* //채점으로 시작되는 모든 변수를 앞자리가 단원으로 시작하게끔..
 
 
